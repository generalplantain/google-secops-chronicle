rule onepassword_multiple_failed_logins_detected {

    meta:
        author = "Sakil Khan"
        description = "Detects multiple failed login attempts to OnePassword. An adversary may attempt to brute force user accounts to gain unauthorized access."
        mitre_attack_tactic = "Credential Access"
        mitre_attack_technique = "Brute Force"
        type = "Alert"
        data_source = "ONEPASSWORD"
        severity = "Medium"
    events:
        $onep.metadata.vendor_name = "ONEPASSWORD"
        $onep.metadata.ingestion_labels.value = "signinattempts"
        $onep.extracted.fields["category"] = "credentials_failed"
        $onep.metadata.event_timestamp.seconds = $timestamp
        $onep.extracted.fields["target_user.name"]=$userid

    match:

        $userid over 10m

    outcome:
        // Risk scoring â€” base: 40 | weekend: +15 | off-hours: +15
        $risk_score = max(
            40 +
            if (01 = timestamp.get_day_of_week($timestamp, "UTC"), 15) + // Sunday
            if (07 = timestamp.get_day_of_week($timestamp, "UTC"), 15) + // Saturday
            if ((timestamp.get_hour($timestamp, "UTC") >= 0 and timestamp.get_hour($timestamp, "UTC") <= 8) or timestamp.get_hour($timestamp, "UTC") > 18, 15) // Off-hours (before 08:00 or after 18:00 UTC)
        )
        // Contextual enrichment for alert triage

        $target_user_email_addresses = array_distinct($onep.target.user.email_addresses)
        $category = array_distinct($onep.extracted.fields["category"])
        $client_app = array_distinct($onep.extracted.fields["client.app_name"])
        $client_os = array_distinct($onep.extracted.fields["client.os_name"])
        $principal_platform = array_distinct($onep.principal.platform)
        $principal_ip = array_distinct($onep.principal.ip)
        $principal_ip_country = array_distinct($onep.principal.ip_geo_artifact.location.country_or_region)
        $principal_ip_state = array_distinct($onep.principal.ip_geo_artifact.location.state)
        $principal_ip_city = array_distinct($onep.principal.location.city)
        $principal_attribute = array_distinct($onep.principal.resource.attribute.labels.key)
        $security_result_description = array_distinct($onep.security_result.description)
        $security_result_summary = array_distinct($onep.security_result.summary)
        $type = array_distinct($onep.extracted.fields["type"])
        $department = array_distinct($onep.target.user.department)
        $count_attempts = count($onep.extracted.fields["category"])
        $dc_user = count_distinct($onep.extracted.fields["target_user.name"])

    condition:
        #onep >= 5
}
