rule google_workspace_admin_role_modified {

    meta:
        author = "Sakil Khan"
        description = "Detects when a custom admin role or its permissions are modified. An adversary may modify a custom admin role in order to elevate the permissions of other user accounts and persist in the target environment."
        mitre_attack_tactic = "Persistence"
        mitre_attack_technique = "Account manipulation"
        mitre_attack_url = "https://attack.mitre.org/techniques/T1098/"
        type = "Alert"
        data_source ="Google Workspace"
        severity = "Medium"
    events:

        $workspace.metadata.vendor_name = "Google Workspace"

        (
        $workspace.metadata.product_event_type = "ADD_PRIVILEGE" or
        $workspace.metadata.product_event_type = "UPDATE_ROLE"
        )

        $workspace.metadata.event_timestamp.seconds = $timestamp
        $workspace.principal.user.userid = $userid

    outcome:
        // Risk scoring â€” base: 40 | weekend: +15 | off-hours: +15
        $risk_score = max(
            40 +
            if (01 = timestamp.get_day_of_week($timestamp, "UTC"), 15) + // Sunday
            if (07 = timestamp.get_day_of_week($timestamp, "UTC"), 15) + // Saturday
            if ((timestamp.get_hour($timestamp, "UTC") >= 0 and timestamp.get_hour($timestamp, "UTC") <= 8) or timestamp.get_hour($timestamp, "UTC") > 18, 15) // Off-hours (before 08:00 or after 18:00 UTC)
        )
        // Contextual enrichment for alert triage

        $principal_user_email_addresses = array_distinct ($workspace.principal.user.email_addresses)
        $principal_user_userid = array_distinct ($workspace.principal.user.userid)
        $mitre_attack_tactic = "Persistence"
        $mitre_attack_technique = "Account manipulation"
        $mitre_attack_url = "https://attack.mitre.org/techniques/T1098/"
        $principal_ip = array_distinct($workspace.principal.ip)
        $principal_ip_country = array_distinct($workspace.principal.ip_geo_artifact.location.country_or_region)
        $security_result_description = array_distinct($workspace.security_result.description)
        $security_result_summary = array_distinct($workspace.security_result.summary)
        $metadata_product_event_type= array_distinct($workspace.metadata.product_event_type)
        $metadata_event_type= array_distinct($workspace.metadata.event_type)
        $metadata_url= array_distinct($workspace.metadata.url_back_to_product)
        $addtional_fields_event_name = array_distinct($workspace.additional.fields["event_name_0"])
        $addtional_fields_event_type = array_distinct($workspace.additional.fields["event_type_0"])
        $addtional_fields_privilege_name = array_distinct($workspace.additional.fields["privilege_name_0_2"])
        $target_resource_resource_type = array_distinct($workspace.target.resource.resource_type)
        $target_user_attribute_roles_name = array_distinct($workspace.target.user.attribute.roles.name)
        $target_application = array_distinct($workspace.target.application)
        $security_result_action = array_distinct($workspace.security_result.action)

    condition:
        $workspace
}
