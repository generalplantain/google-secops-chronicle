rule google_drive_ownership_transferred_via_google_workspace {

    meta:
        author = "Sakil Khan"
        description = "Drive and Docs is a Google Workspace service that allows users to leverage Google Drive and Google Docs. Access to files is based on inherited permissions from the child organizational unit the user belongs to which is scoped by administrators. Typically if a user is removed, their files can be transferred to another user by the administrator. This service can also be abused by adversaries to transfer files to an adversary account for potential exfiltration."
        mitre_attack_tactic = "Collection"
        mitre_attack_technique = "Data Staged"
        type = "Alert"
        data_source = "Google Workspace"
        severity = "Medium"
    events:
        $drive.metadata.vendor_name = "Google Workspace"
        $drive.metadata.event_timestamp.seconds = $timestamp
        $drive.metadata.product_event_type = "CREATE_DATA_TRANSFER_REQUEST"
        $drive.principal.user.email_addresses = $userid
        $drive.src.user.email_addresses = $sourceuser
        $drive.target.user.email_addresses = $targetuser

    match:
        $sourceuser, $targetuser, $userid over 1h

    outcome:
        // Risk scoring â€” base: 40 | weekend: +15 | off-hours: +15
        $risk_score = max(
            40 +
            if (01 = timestamp.get_day_of_week($timestamp, "UTC"), 15) + // Sunday
            if (07 = timestamp.get_day_of_week($timestamp, "UTC"), 15) + // Saturday
            if ((timestamp.get_hour($timestamp, "UTC") >= 0 and timestamp.get_hour($timestamp, "UTC") <= 8) or timestamp.get_hour($timestamp, "UTC") > 18, 15) // Off-hours (before 08:00 or after 18:00 UTC)
        )
        // Contextual enrichment for alert triage

        $principal_ip = array_distinct($drive.principal.ip)
        $principal_ip_country = array_distinct($drive.principal.ip_geo_artifact.location.country_or_region)
        $principal_ip_state = array_distinct($drive.principal.ip_geo_artifact.location.state)
        $principal_ip_city = array_distinct($drive.principal.location.city)
        $admin_user = array_distinct($drive.principal.user.email_addresses)
        $target_email = array_distinct($drive.target.user.email_addresses)
        $source_user = array($drive.src.user.email_addresses)

    condition:
        $drive
}
