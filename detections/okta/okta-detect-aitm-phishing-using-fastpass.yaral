rule okta_detect_aitm_phishing_using_fastpass {

    meta:
        author = "Sakil Khan"
        description = "Detects unsuccessful phishing attempts using FastPass. When Okta users are enrolled in FastPass (Okta's password-less solution), Okta can provide defenders a high-fidelity signal for when  user applications are being targeted by attackers wielding real-time (AiTM) proxies."
        mitre_attack_tactic = "Initial Access"
        mitre_attack_technique = "Phishing"
        mitre_attack_url = "https://attack.mitre.org/techniques/T1566/"
        type = "Alert"
        data_source = "Okta"
        severity = "Medium"
    events:
        $fastpass.metadata.vendor_name = "Okta"
        $fastpass.metadata.product_event_type = "user.authentication.auth_via_mfa"
        $fastpass.extracted.fields["outcome.reason"] = "FastPass declined phishing attempt"
        $fastpass.metadata.event_timestamp.seconds = $timestamp
        $fastpass.principal.user.userid = $userid

    outcome:
        // Risk scoring â€” base: 40 | weekend: +15 | off-hours: +15
        $risk_score = max(
            40 +
            if (01 = timestamp.get_day_of_week($timestamp, "UTC"), 15) + // Sunday
            if (07 = timestamp.get_day_of_week($timestamp, "UTC"), 15) + // Saturday
            if ((timestamp.get_hour($timestamp, "UTC") >= 0 and timestamp.get_hour($timestamp, "UTC") <= 8) or timestamp.get_hour($timestamp, "UTC") > 18, 15) // Off-hours (before 08:00 or after 18:00 UTC)
        )
        $principal_user_email_addresses = array_distinct ($fastpass.principal.user.email_addresses)
        $mitre_attack_tactic = "Initial Access"
        $mitre_attack_technique = "Phishing"
        $mitre_attack_url = "https://attack.mitre.org/techniques/T1566/"
        $target_user_agent = array_distinct($fastpass.network.http.user_agent)
        $client_user_agent_browser = array_distinct($fastpass.extracted.fields["client.userAgent.browser"])
        $client_user_agent_os = array_distinct($fastpass.extracted.fields["client.userAgent.os"])
        $principal_ip = array_distinct($fastpass.principal.ip)
        $principal_ip_country = array_distinct($fastpass.principal.ip_geo_artifact.location.country_or_region)
        $principal_ip_state = array_distinct($fastpass.principal.ip_geo_artifact.location.state)
        $principal_ip_city = array_distinct($fastpass.principal.location.city)
        $security_result_description = array_distinct($fastpass.security_result.description)
        $security_result_summary = array_distinct($fastpass.security_result.summary)
        $debug_context_data_behaviours = array_distinct($fastpass.extracted.fields["debugContext.debugData.behaviors"])
        $outcome_result = array_distinct($fastpass.extracted.fields["outcome.result"])
        $outcome_reason = array_distinct($fastpass.extracted.fields["outcome.reason"])

    condition:
        $fastpass
}
