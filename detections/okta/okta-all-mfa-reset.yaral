rule okta_all_mfa_reset {

    meta:
        author = "Sakil Khan"
        description = "Detects when all multi-factor authentication settings are reset."
        mitre_attack_tactic = "Credential Access"
        mitre_attack_technique = "Valid Accounts"
        mitre_attack_url = "https://attack.mitre.org/techniques/T1078/"
        type = "Alert"
        data_source = "Okta"
        severity = "Critical"
    events:
        $reset.metadata.vendor_name = "Okta"
        $reset.metadata.product_event_type = "user.mfa.factor.reset_all"
        $reset.metadata.event_timestamp.seconds = $timestamp
        $reset.principal.user.userid = $userid

    outcome:
        // Risk scoring â€” base: 70 | weekend: +15 | off-hours: +15
        $risk_score = max(
            70 +
            if (01 = timestamp.get_day_of_week($timestamp, "UTC"), 15) + // Sunday
            if (07 = timestamp.get_day_of_week($timestamp, "UTC"), 15) + // Saturday
            if ((timestamp.get_hour($timestamp, "UTC") >= 0 and timestamp.get_hour($timestamp, "UTC") <= 8) or timestamp.get_hour($timestamp, "UTC") > 18, 15) // Off-hours (before 08:00 or after 18:00 UTC)
        )
        $principal_user_email_addresses = array_distinct ($reset.principal.user.email_addresses)
        $mitre_attack_tactic = "Credential Access"
        $mitre_attack_technique = "Valid Accounts"
        $mitre_attack_url = "https://attack.mitre.org/techniques/T1078/"
        $target_user_agent = array_distinct($reset.network.http.user_agent)
        $client_user_agent_browser = array_distinct($reset.extracted.fields["client.userAgent.browser"])
        $client_user_agent_os = array_distinct($reset.extracted.fields["client.userAgent.os"])
        $principal_ip = array_distinct($reset.principal.ip)
        $principal_ip_country = array_distinct($reset.principal.ip_geo_artifact.location.country_or_region)
        $principal_ip_state = array_distinct($reset.principal.ip_geo_artifact.location.state)
        $principal_ip_city = array_distinct($reset.principal.location.city)
        $security_result_description = array_distinct($reset.security_result.description)
        $security_result_summary = array_distinct($reset.security_result.summary)
        $debug_context_data_behaviours = array_distinct($reset.extracted.fields["debugContext.debugData.behaviors"])
        $outcome_result = array_distinct($reset.extracted.fields["outcome.result"])

    condition:
        $reset
}
