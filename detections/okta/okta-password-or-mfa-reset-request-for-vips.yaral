rule okta_password_or_mfa_reset_request_for_vips {

  meta:
    author = "Sakil Khan"
    description = "Detects when Okta password or MFA resets are requested for VIP users. Helps identify targeted credential attacks against high-value accounts."
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Valid Accounts"
    type = "Alert"
    data_source = "Okta"
    severity = "Medium"
    false_positives = "This rule may cause false positives if requests are performed for legitimate use cases."
  events:

    $reset.metadata.vendor_name = "Okta"
    $reset.metadata.event_timestamp.seconds = $timestamp
    $userid = $reset.extracted.fields["actor.displayName"]

    (
    $reset.metadata.product_event_type = "system.email.password_reset.sent_message" or
    $reset.metadata.product_event_type = "system.sms.send_password_reset_message" or
    $reset.metadata.product_event_type = "system.voice.send_password_reset_call" or
    $reset.metadata.product_event_type = "user.account.reset_password" or
    $reset.metadata.product_event_type = "user.account.update_password" or
    $reset.metadata.product_event_type = "user.mfa.factor.deactivate" or
    $reset.metadata.product_event_type ="user.mfa.factor.update" or
    $reset.metadata.product_event_type = "user.mfa.factor.suspend" or
    $reset.metadata.product_event_type = "user.mfa.factor.unsuspend" or
    $reset.metadata.product_event_type = "user.mfa.attempt_bypass"
        )

    strings.concat($reset.extracted.fields["actor.alternateId"] , "") in %vip_users

    match:
         $userid over 1h

    outcome:

        // Risk scoring â€” base: 40 | weekend: +15 | off-hours: +15
        $risk_score = max(
            40 +
            if (01 = timestamp.get_day_of_week($timestamp, "UTC"), 15) + // Sunday
            if (07 = timestamp.get_day_of_week($timestamp, "UTC"), 15) + // Saturday
            if ((timestamp.get_hour($timestamp, "UTC") >= 0 and timestamp.get_hour($timestamp, "UTC") <= 8) or timestamp.get_hour($timestamp, "UTC") > 18, 15) // Off-hours (before 08:00 or after 18:00 UTC)
        )
    $principal_user_email_addresses = array_distinct ($reset.principal.user.email_addresses)
    $mitre_attack_tactic = "Credential Access"
    $mitre_attack_technique = "Valid Accounts"
    $target_user_agent = array_distinct($reset.network.http.user_agent)
    $client_user_agent_browser = array_distinct($reset.extracted.fields["client.userAgent.browser"])
    $client_user_agent_os = array_distinct($reset.extracted.fields["client.userAgent.os"])
    $principal_ip = array_distinct($reset.principal.ip)
    $principal_ip_country = array_distinct($reset.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($reset.principal.ip_geo_artifact.location.state)
    $principal_ip_city = array_distinct($reset.principal.location.city)
    $security_result_description = array_distinct($reset.security_result.description)
    $security_result_summary = array_distinct($reset.security_result.summary)
    $debug_context_data_behaviours = array_distinct($reset.extracted.fields["debugContext.debugData.behaviors"])
    $outcome_result = array_distinct($reset.extracted.fields["outcome.result"])
    $display_message= array_distinct($reset.extracted.fields["displayMessage"])
    $eventType = array_distinct($reset.extracted.fields["eventType"])

  condition:
    $reset
}
